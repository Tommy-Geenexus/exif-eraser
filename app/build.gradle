import io.gitlab.arturbosch.detekt.Detekt

plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.dagger.hilt)
    alias(libs.plugins.detekt)
    alias(libs.plugins.kotlin)
    alias(libs.plugins.kotlin.kapt)
    alias(libs.plugins.kotlin.parcelize)
    alias(libs.plugins.ktlint)
    alias(libs.plugins.navigation.safe.args)
    alias(libs.plugins.spotless)
    alias(libs.plugins.versions)
    alias(libs.plugins.wire)
}

android {
    namespace 'com.none.tom.exiferaser'
    compileSdkVersion 33

    defaultConfig {
        applicationId "com.none.tom.exiferaser"
        minSdkVersion 27
        targetSdkVersion 33
        versionCode 31
        versionName "5.2.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"),
                    "proguard-rules.pro"
        }
    }

    buildFeatures {
        viewBinding true
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        resources {
            excludes += ['META-INF/AL2.0', 'META-INF/LGPL2.1']
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

wire {
    kotlin {
        android = true
    }
}

tasks.named("dependencyUpdates").configure {
    def isNonStable = { String version ->
        def stableKeyword = ["RELEASE", "FINAL", "GA"].any { keyword ->
            version.toUpperCase().contains(keyword)
        }
        def regex = /^[0-9,.v-]+(-r)?$/
        return !stableKeyword && !(version ==~ regex)
    }
    rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }
}

tasks.withType(Detekt).configureEach {
    jvmTarget = "1.8"
}

detekt {
    baseline = file("$projectDir/config/baseline.xml")
}

ktlint {
    android.set(true)
    disabledRules.set(["import-ordering"])
    filter {
        exclude { element -> element.file.path.contains("generated/") }
        include("**/*.kt")
    }
}

spotless {
    kotlin {
        target("**/*.kt")
        targetExclude("**/AnimationUtils.kt")
        //noinspection GroovyImplicitNullArgumentCall
        ktlint()
        endWithNewline()
        licenseHeaderFile rootProject.file("spotless/copyright.txt")
    }
}

dependencies {
    androidTestImplementation libs.bundles.implementation.test.android
    coreLibraryDesugaring libs.desugar.jdk.libs
    debugImplementation libs.leakcanary
    implementation libs.bundles.implementation
    kapt libs.bundles.kapt
    testImplementation libs.bundles.implementation.test
}
